apply plugin: 'com.android.application'

import java.text.DateFormat
import java.text.SimpleDateFormat
import com.android.build.OutputFile

def getDateTimestamp() {
    DateFormat df = new SimpleDateFormat("YYYY-MM-DD-HH:mm:ss");
    return new String("BuildTime=" + df.format(new Date()).toString());
}

def getShellCommandOutput(String shellCmd) {
    def cmdResult = ['bash', '-c', shellCmd].execute();
    cmdResult.waitFor();
    return cmdResult.text[0..-2];
}

def getGitCommitHash() {
    return getShellCommandOutput("git show | head -n 1 | sed -e \"s/commit //\" | cut -c-12");
}

def getGitCommitDate() {
    return getShellCommandOutput("git show | grep Date: | head -n 1 | sed -e \"s/Date:[ ]*//\"");
}

android {

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.maxieds.chameleonminilivedebugger"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 68
        versionName "1.0.6"
        //multiDexEnabled true

        buildConfigField "String", "GIT_COMMIT_HASH", "\"" + getGitCommitHash() + "\""
        buildConfigField "String", "GIT_COMMIT_DATE", "\"" + getGitCommitDate() + "\""
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            crunchPngs false
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            crunchPngs false
        }
    }
    buildToolsVersion '28.0.3'

    flavorDimensions "mode"
    def configBuildTimeStamp = getDateTimestamp()
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def directoryPath = "all-builds/"
            def apkName = "ChameleonMiniLiveDebugger-${variant.name}-vc${variant.versionCode}-v${variant.versionName}-${configBuildTimeStamp}.apk"
        }
    }
    productFlavors {
        free {
            versionNameSuffix "-free"
            dimension "mode"
            buildConfigField "boolean", "PAID_APP_VERSION", "false"
            buildConfigField "boolean", "REPORT_CRASHES", "true"
            buildConfigField "String", "BUILD_TIMESTAMP", "\"" + configBuildTimeStamp + "\""
            resConfigs "en", "en_US", "en_UK", "de"
            manifestPlaceholders = [
                    appIcon        : "@drawable/chameleonicon_about64_roundicon",
                    appRoundIcon   : "@drawable/chameleonicon_about64_roundicon",
                    appTheme       : "@style/AppThemeGreen",
                    appDebug       : "false",
                    installLocation: "internalOnly"
            ]
        }
        paid {
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
            buildConfigField "boolean", "PAID_APP_VERSION", "true"
            buildConfigField "boolean", "REPORT_CRASHES", "false"
            buildConfigField "String", "BUILD_TIMESTAMP", "\"" + configBuildTimeStamp + "\""
            resConfigs "en", "en_US", "en_UK", "de"
            dimension "mode"
            manifestPlaceholders = [
                    appIcon        : "@drawable/chameleonicon64_paid",
                    appRoundIcon   : "@drawable/chameleonicon64_paid",
                    appTheme       : "@style/AppThemeGreen",
                    appDebug       : "false",
                    installLocation: "auto"
            ]
        }

        packagingOptions {
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/notice.txt'
            exclude 'META-INF/ASL2.0'
            exclude 'META-INF/androidx*'
            exclude 'META-INF/android*'
        }

    }

    dependencies {
        implementation fileTree(include: ['*.jar'], dir: 'libs')
        implementation 'com.android.support.constraint:constraint-layout:1.1.3'
        testImplementation 'junit:junit:4.12'
        implementation 'androidx.legacy:legacy-support-v4:1.0.0'
        implementation 'androidx.appcompat:appcompat:1.0.2'
        implementation 'com.google.android.material:material:1.1.0-alpha07'
        implementation 'com.github.felHR85:UsbSerial:4.5.2'
        implementation 'com.akexorcist:bluetoothspp:1.0.0'
        implementation 'com.shawnlin:number-picker:2.4.9'
        implementation 'com.github.maxieds:MifareClassicToolLibrary:1.1.8'
    }

    repositories {
        jcenter()
        google()
        mavenCentral()
        maven {
            url "https://maven.google.com"
            name 'Google'
        }
        maven { url "https://jitpack.io" }
    }

}